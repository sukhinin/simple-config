plugins {
    id 'maven-publish'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.6.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'com.github.sukhinin'
description 'Yet another configuration library for Kotlin'

repositories {
    mavenCentral()
    maven {
        url 'https://maven.pkg.github.com/sukhinin'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.0'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

test {
    useJUnitPlatform()
}

jgitver {
    strategy 'MAVEN'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sukhinin/simple-config")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
