plugins {
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.6.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

group 'com.github.sukhinin'
description 'Yet another configuration library for Kotlin'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.0'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

test {
    useJUnitPlatform()
}

jgitver {
    useDirty true
    useGitCommitID true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(kotlinSourcesJar)
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/sukhinin/simple-config'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://github.com/sukhinin/simple-config/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sukhinin'
                        name = 'Boris Sukhinin'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/sukhinin/simple-config.git'
                    developerConnection = 'scm:git:https://github.com/sukhinin/simple-config.git'
                    url = 'https://github.com/sukhinin/simple-config'
                }
            }
        }
    }
}

bintray {
    user = project.findProperty('bintray.user') ?: System.getenv('BINTRAY_USER')
    key = project.findProperty('bintray.apikey') ?: System.getenv('BINTRAY_APIKEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/sukhinin/simple-config'
        issueTrackerUrl = 'https://github.com/sukhinin/simple-config/issues'
        vcsUrl = 'https://github.com/sukhinin/simple-config.git'
        licenses = ['Apache-2.0']
        labels = ['simple', 'kotlin', 'config']
        githubRepo = 'sukhinin/simple-config'
    }
}
